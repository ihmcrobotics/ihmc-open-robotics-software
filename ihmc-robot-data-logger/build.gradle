buildscript {
   repositories {
      maven { url "https://plugins.gradle.org/m2/" }
      maven { url "http://dl.bintray.com/ihmcrobotics/maven-release" }
      maven { url "http://dl.bintray.com/ihmcrobotics/maven-vendor" }
   }
   dependencies {
      classpath group: "us.ihmc", name: "ihmc-pub-sub-generator", version: "0.10.2"
   }
}

plugins {
   id("us.ihmc.ihmc-build") version "0.18.0"
   id("us.ihmc.ihmc-ci") version "4.25"
   id("us.ihmc.ihmc-cd") version "0.1"
   id("us.ihmc.log-tools") version "0.3.1"
   id("org.hidetake.ssh") version "2.9.0"
   id "application"
}

import org.gradle.api.GradleException

ihmc {
   loadProductProperties("../product.properties")

   configureDependencyResolution()
   resourceDirectory("main", "idl")
   configurePublications()
}

sourceSets.main.java.srcDirs += ["src/main/java-generated"]

mainDependencies {
   compile("com.google.protobuf:protobuf-java:2.6.1")
   compile("net.sf.trove4j:trove4j:3.0.3")
   compile("com.martiansoftware:jsap:2.1")
   compile("org.apache.commons:commons-lang3:3.8.1")
   compile("com.google.guava:guava:18.0")
   compile("commons-io:commons-io:2.6")
   compile("org.tukaani:xz:1.5")
   compile("org.xerial.snappy:snappy-java:1.1.1-M1")
   compile("net.jpountz.lz4:lz4:1.3.0")
   compile("io.netty:netty-all:4.1.32.Final")

   compile("us.ihmc:euclid:0.12.0")
   compile("us.ihmc:ihmc-yovariables:0.3.11")
   compile("us.ihmc:ihmc-video-codecs:2.1.5")
   compile("us.ihmc:ihmc-realtime:1.2.6")
   compile("us.ihmc:ihmc-java-decklink-capture:0.3.1")
   compile("us.ihmc:ihmc-pub-sub:0.10.2")
   compile("us.ihmc:ihmc-pub-sub-serializers-extra:0.10.2")
   compile("us.ihmc:ihmc-commons:0.26.6")
   compile("us.ihmc:ihmc-graphics-description:0.12.12")
   compile("us.ihmc:ihmc-robot-description:0.12.7")
   compile("us.ihmc:mecano:0.0.21")
   compile("us.ihmc:mecano-yovariables:0.0.21")
   
   compile("us.ihmc:ihmc-model-file-loader:source")
   
   compile("org.openjdk.jol:jol-core:0.9")
   
}

testDependencies {

   compile("us.ihmc:ihmc-commons-testing:0.26.6")
   compile("us.ihmc:ihmc-robotics-toolkit-test:source")
}

mainClassName = hasProperty("mainClass") ? mainClass : "us.ihmc.robotDataLogger.logger.YoVariableLoggerDispatcher"

if (ihmc.isBuildRoot())
{
   task loggerStartScripts(type: org.gradle.jvm.application.tasks.CreateStartScripts) {
      outputDir = file("build/scripts")
      mainClassName = "us.ihmc.robotDataLogger.logger.YoVariableLoggerDispatcher"
      applicationName = "IHMCLogger"
      classpath = project.configurations.runtime + jar.outputs.files
   }

   task generateMessages(type: us.ihmc.idl.generator.IDLGeneratorTask) {
      idlFiles = fileTree(dir: "src/main/idl")
      includeDirs = files(".")
      targetDirectory = file("src/main/java-generated")
      packagePrefix = ""
   }

   compileJava.dependsOn generateMessages

   distributions {
      logger {
         baseName = "IHMCLogger"
         contents {
            into("lib") {
               from project.configurations.runtime + jar.outputs.files
            }

            into("bin") {
               from loggerStartScripts
               include "IHMCLogger*"
            }
         }
      }
   }

   task setupDeployLoggerRemote {
      doLast {
         remotes.create("deployLoggerTarget") {
            host = deployLoggerHost
            user = deployLoggerUser
            password = deployLoggerPassword
            knownHosts = allowAnyHosts
         }
      }
   }

   task deployLogger(dependsOn: [loggerDistTar, setupDeployLoggerRemote]) {
      doLast {
         ssh.run {
            session(remotes.deployLoggerTarget) {
               project.logger.lifecycle("Copying Logger distribution tarball to remote host")
               def distTarFile = loggerDistTar.outputs.files.singleFile
               put from: distTarFile, into: "."

               project.logger.lifecycle("Untarring distribution on remote host")
               execute "tar xf ./${distTarFile.name}"
               project.logger.lifecycle("Removing tarball from remote host")
               execute "rm -f ./${distTarFile.name}"
               project.logger.lifecycle("Removing old version")
               execute "rm -rf IHMCLogger"
               project.logger.lifecycle("Moving Logger distribution in to place")
               execute "mv ./${distTarFile.name.replace(".tar", "")} IHMCLogger"
               project.logger.lifecycle("Logger deployment to remote host complete!")
            }
         }
      }
   }

   task checkThatDistributionDoesntAlreadyExist(type: Exec) {
      def distTarFile = loggerDistTar.outputs.files.singleFile
      workingDir project.projectDir
      executable "curl"
      args = ["--write-out", "%{http_code}", "--silent", "--output", "/dev/null", "--head", "https://dl.bintray.com/ihmcrobotics/distributions/${distTarFile.name}"]
      standardOutput = new ByteArrayOutputStream();

      doLast {
         execResult.assertNormalExitValue()
         def output = standardOutput.toString()
         if (output.equals("200"))
         {
            throw new GradleException("Distribution ${distTarFile.name} already exists on Bintray. Distributions versions should not be overwritten. Did you mean to release a new version or hotfix?")
         }
      }
   }

   task publishLoggerDistributionToBintray(type: Exec, dependsOn: [checkThatDistributionDoesntAlreadyExist, loggerDistTar]) {
      def distTarFile = loggerDistTar.outputs.files.singleFile

      workingDir project.projectDir
      executable "curl"
      args = ["--write-out", "%{http_code}", "--silent", "--output", "/dev/null", "-T", distTarFile.canonicalPath, "-u${bintray_user}:${bintray_key}", "https://api.bintray.com/content/ihmcrobotics/distributions/IHMCLogger/${project.version}/${distTarFile.name}?publish=1"]
      standardOutput = new ByteArrayOutputStream();

      doLast {
         execResult.assertNormalExitValue()
         def output = standardOutput.toString()
         if (!output.equals("201"))
         {
            throw new GradleException("Upload failed! HTTP Response code: ${output}.")
         }
      }
   }
}