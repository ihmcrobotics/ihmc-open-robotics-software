buildscript {
   repositories {
      mavenCentral()
      maven { url "http://dl.bintray.com/ihmcrobotics/maven-release" }
      maven { url "http://dl.bintray.com/ihmcrobotics/maven-vendor" }
      maven { url "https://plugins.gradle.org/m2/" }
      mavenLocal()
      jcenter()
   }
   dependencies {
      classpath "us.ihmc:ihmc-build:0.13.0"
      classpath "us.ihmc:ros2-msg-to-pubsub-generator:0.7.0-alpha"
//      classpath "us.ihmc:ihmc-pub-sub-generator:0.6.9-alpha"
      classpath "org.ajoberstar:grgit:2.1.0"
      classpath "com.bmuschko:gradle-docker-plugin:3.2.5"
   }
}

apply plugin: "us.ihmc.ihmc-build"
apply plugin: "org.ajoberstar.grgit"
apply plugin: "com.bmuschko.docker-remote-api"

ihmc {
   loadProductProperties("../product.properties")

   configureDependencyResolution()
   resourceDirectory("main", "messages")
   resourceDirectory("main", "generated-idl")
   javaDirectory("main", "generated-java")
   resourceDirectory("generator", "docker")
   configurePublications()
}

testSuites {
   def prefix = "LIBS-IHMCOPENROBOTICSSOFTWARE"
   bambooPlanKeys = [prefix + "FAST", prefix + "INDEVELOPMENT", prefix + "UI", prefix + "VIDEO"]
}

mainDependencies {
   compile group: 'us.ihmc', name: 'euclid-core', version: '0.5.1'
   compile group: 'us.ihmc', name: 'euclid', version: '0.8.2'
   compile group: 'us.ihmc', name: 'ihmc-pub-sub', version: '0.6.9-alpha'
   compile group: 'us.ihmc', name: 'ros2-common-interfaces', version: '0.7.0-alpha'
}

testDependencies {
   compile group: 'us.ihmc', name: 'ihmc-ros2-library', version: '0.7.0-alpha'
}

generatorDependencies {
   compile group: "us.ihmc", name: "ihmc-commons", version: "0.18.0"
   compile group: "us.ihmc", name: "ros2-msg-to-pubsub-generator", version: "0.7.0-alpha"
}

task buildImage(type: com.bmuschko.gradle.docker.tasks.image.DockerBuildImage) {
   inputDir = file("src/generator/docker")
   tag = "ihmc/ros1-bridge-build"
}

task generateMessages {
   doFirst {
      delete("src/main/generated-idl")
      delete("src/main/generated-java")
      delete("build/tmp/generateMessages")

      def foundDependency = false
      copy {
         configurations.compile.findAll { it.name.contains("ros2-common-interfaces") }.each {
            from zipTree(it)
            foundDependency = true
         }
         into file("build/tmp/generateMessages/ros2-common-interfaces")
      }

      if (!foundDependency)
      {
         throw new GradleException("Could not find ros2-common-interfaces in configurations.compile!")
      }

      def generator = new us.ihmc.ros2.rosidl.RosInterfaceGenerator();

      generator.addPackageRoot(file("build/tmp/generateMessages/ros2-common-interfaces/rcl_interfaces").toPath());
      generator.addPackageRoot(file("build/tmp/generateMessages/ros2-common-interfaces/common_interfaces").toPath());
      generator.addPackageRoot(file("src/main/messages/ihmc_interfaces").toPath());

      generator.addCustomIDLFiles(file("build/tmp/generateMessages/ros2-common-interfaces/").toPath());

      generator.generate(file("build/tmp/generateMessages/generated-idl").toPath(), file("build/tmp/generateMessages/generated-java").toPath());

      copy {
         from "build/tmp/generateMessages/generated-idl/controller_msgs"
         into "src/main/generated-idl/controller_msgs"
      }

      copy {
         from "build/tmp/generateMessages/generated-java/controller_msgs"
         into "src/main/generated-java/controller_msgs"
      }

      generator.convertDirectoryToUnixEOL(file("src/main/generated-idl").toPath())
      generator.convertDirectoryToUnixEOL(file("src/main/generated-java").toPath())
   }
}
