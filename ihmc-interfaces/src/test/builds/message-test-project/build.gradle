buildscript {
   repositories {
      maven { url "https://plugins.gradle.org/m2/" }
      mavenCentral()
      maven { url "https://dl.bintray.com/ihmcrobotics/maven-release" }
      maven { url "https://dl.bintray.com/ihmcrobotics/maven-vendor" }
      mavenLocal()
      jcenter()
   }
   dependencies {
      classpath "us.ihmc:ros2-msg-to-pubsub-generator:0.15.3"
   }
}

plugins {
   id("us.ihmc.ihmc-build") version "0.20.1"
   id("us.ihmc.log-tools") version "0.3.1"
}

ihmc {
   group = "us.ihmc"
   version = "0.0"

   configureDependencyResolution()
   resourceDirectory("main", "messages")
   resourceDirectory("main", "generated-idl")
   javaDirectory("main", "generated-java")
   configurePublications()
}

mainDependencies {
   api("us.ihmc:ihmc-pub-sub:0.11.0")
   api("us.ihmc:ros2-common-interfaces:0.15.3")
}

def generator = new us.ihmc.ros2.rosidl.RosInterfaceGenerator();

task generateMessages {
   doFirst {
      delete("src/main/generated-idl")
      delete("src/main/generated-java")
      delete("build/tmp/generateMessages")

      def foundDependency = false
      copy {
         configurations.default.findAll { it.name.contains("ros2-common-interfaces") }.each {
            from zipTree(it)
            foundDependency = true
         }
         into file("build/tmp/generateMessages/ros2-common-interfaces")
      }

      if (!foundDependency)
      {
         throw new org.gradle.api.GradleException("Could not find ros2-common-interfaces in configurations.default!")
      }

      generator.addPackageRootToIDLGenerator(file("build/tmp/generateMessages/ros2-common-interfaces/rcl_interfaces").toPath());
      generator.addPackageRootToIDLGenerator(file("build/tmp/generateMessages/ros2-common-interfaces/common_interfaces").toPath());
      generator.addPackageRootToIDLGenerator(file("src/main/messages/ihmc_interfaces").toPath());
      generator.addPackageRootToROS1Generator(file("src/main/messages/ihmc_interfaces").toPath());

      generator.addCustomIDLFiles(file("build/tmp/generateMessages/ros2-common-interfaces/").toPath());

      generator.generate(file("build/tmp/generateMessages/generated-idl").toPath(), file("build/tmp/generateMessages/generated-ros1").toPath(), file("build/tmp/generateMessages/generated-java").toPath());

      copy {
         from "build/tmp/generateMessages/generated-idl/generator_test_msgs"
         into "src/main/generated-idl/generator_test_msgs"
      }

      copy {
         from "build/tmp/generateMessages/generated-java/generator_test_msgs"
         into "src/main/generated-java/generator_test_msgs"
      }

      copy {
         from "build/tmp/generateMessages/generated-ros1/generator_test_msgs"
         into "src/main/messages/ros1/generator_test_msgs"
      }

      generator.convertDirectoryToUnixEOL(file("src/main/generated-idl").toPath())
      generator.convertDirectoryToUnixEOL(file("src/main/generated-java").toPath())
      generator.convertDirectoryToUnixEOL(file("src/main/messages/ros1").toPath())
   }
}
