plugins {
   id("us.ihmc.ihmc-build") version "0.18.5"
   id("us.ihmc.ihmc-ci") version "4.25"
   id("us.ihmc.ihmc-cd") version "0.1"
   id("us.ihmc.scs") version "0.4"
   id("us.ihmc.log-tools") version "0.3.1"
   id("org.hidetake.ssh") version "2.9.0"
   id "application"
}

ihmc {
   loadProductProperties("../product.properties")

   configureDependencyResolution()
   configurePublications()
}

mainDependencies {
   compile("com.martiansoftware:jsap:2.1")
   compile("org.yaml:snakeyaml:1.17") //1.11
   compile("org.ejml:core:0.30")
   compile("org.ejml:simple:0.30")
   compile("org.ejml:dense64:0.30")
   compile("com.esotericsoftware.minlog:minlog:1.2")
   compile("org.apache.commons:commons-lang3:3.8.1")
   compile("us.ihmc.thirdparty.jinput:jinput:190214")
   compile("org.ros.rosjava_bootstrap:message_generation:0.2.1")
   compile("org.ros.rosjava_messages:std_msgs:0.5.9")

   compile("us.ihmc:euclid:0.12.0")
   compile("us.ihmc:ihmc-yovariables:0.3.11")
   compile("us.ihmc:ihmc-realtime:1.2.6")
   compile("us.ihmc:IHMCRosControl:0.5.0") {
      changing = true
   } 
   compile("us.ihmc:ihmc-commons:0.26.6")
   compile("us.ihmc:ihmc-jmonkey-engine-toolkit:0.12.8")
   compile("us.ihmc:simulation-construction-set:0.12.15")
   compile("us.ihmc:ihmc-graphics-description:0.12.12")
   compile("us.ihmc:ihmc-robot-description:0.12.7")
   compile("us.ihmc:ihmc-communication:source")
   compile("us.ihmc:ihmc-humanoid-robotics:source")
   compile("us.ihmc:ihmc-system-identification:source")
   compile("us.ihmc:ihmc-state-estimation:source")
   compile("us.ihmc:ihmc-common-walking-control-modules:source")
   compile("us.ihmc:ihmc-avatar-interfaces:source")
   compile("us.ihmc:ihmc-ros-tools:source")
   compile("us.ihmc:ihmc-robot-data-logger:source")
   compile("us.ihmc:ihmc-model-file-loader:source")
   compile("us.ihmc:ihmc-sensor-processing:source")
   compile("us.ihmc:ihmc-perception:source")
   compile("us.ihmc:ihmc-whole-body-controller:source")
   compile("us.ihmc:ihmc-java-toolkit:source")
   compile("us.ihmc:ihmc-robotics-toolkit:source")
   compile("us.ihmc:ihmc-robot-models:source")
   compile("us.ihmc:ihmc-robot-data-visualizer:source")
   compile("us.ihmc:ihmc-simulation-toolkit:source")
   compile("us.ihmc:ihmc-footstep-planning-visualizers:source")
   compile("us.ihmc:ihmc-avatar-interfaces-behavior-fx-ui:source")
}

testDependencies {

   compile("us.ihmc:ihmc-commons-testing:0.26.6")
   compile("us.ihmc:ihmc-robotics-toolkit-test:source")
   compile("us.ihmc:ihmc-avatar-interfaces-test:source")
}

applicationName = "IHMCValkyrieJoystickApplication"
mainClassName = "us.ihmc.valkyrie.joystick.ValkyrieJoystickBasedSteppingApplication"

task deployOCUApplications(dependsOn: installDist) {
   doLast {
      def ocuApplications = "ihmc_apps/valkyrie"
      def home = new File(System.properties["user.home"])
      def dest = new File(home, ocuApplications)
      dest.deleteDir();
      dest.mkdirs();
      copy {
         from "${buildDir}/install/" + applicationName
         into dest
      }

      println "-------------------------------------------------------------------------"
      println "------- Deployed files to: " + dest.getPath() + " -------"
      println "-------------------------------------------------------------------------"
   }
}

if (ihmc.isBuildRoot())
{
   remotes {
      valkyrieRealtimeComputer {
         host = project.hasProperty("valkyrie_link_ip") ? "${valkyrie_link_ip}" : "invalid"

         // Set the username and password in ~/.gradle/gradle.properties.
         user = project.hasProperty("valkyrie_realtime_username") ? "${valkyrie_realtime_username}" : "invalid"
         password = project.hasProperty("valkyrie_realtime_password") ? "${valkyrie_realtime_password}" : "invalid"

         knownHosts = allowAnyHosts
      }

      valkyrieNonRealtimeComputer {
         host = project.hasProperty("valkyrie_zelda_ip") ? "${valkyrie_zelda_ip}" : "invalid"

         // Set the username and password in ~/.gradle/gradle.properties.
         user = project.hasProperty("valkyrie_realtime_username") ? "${valkyrie_realtime_username}" : "invalid"
         password = project.hasProperty("valkyrie_realtime_password") ? "${valkyrie_realtime_password}" : "invalid"

         knownHosts = allowAnyHosts
      }
   }

   task deployLocal(dependsOn: jar) {
      ihmc.jarWithLibFolder()
      def directory = "valkyrie"

      doLast {
         def home = new File(System.properties["user.home"])
         def dest = new File(home, directory)
         def configurationDir = new File(home, ".ihmc/Configurations")
         def lib = new File(dest, "lib")
         lib.deleteDir();
         lib.mkdirs();

         configurations.runtime.each {
            if (!it.name.contains("IHMCHumanoidOperatorInterface"))
            {
               def src = file(it)
               copy {
                  from src
                  into lib
               }
            }
         }

         copy {
            from jar.archivePath
            into dest
            rename { fileName -> "ValkyrieController.jar" }
         }

         delete configurationDir
         mkdir configurationDir
         copy {
             from file("saved-configurations/defaultREAModuleConfiguration.txt")
             into configurationDir
         }
      }
   }

   task deploy(dependsOn: jar) {
      def projectDir = project.buildscript.sourceFile.parent
      def directory = "valkyrie"

      doLast {
         if (!project.hasProperty("valkyrie_realtime_username") || !project.hasProperty("valkyrie_realtime_password"))
         {
            println("Please set valkyrie_realtime_username and valkyrie_realtime_password in ~/.gradle/gradle.properties. See https://docs.gradle.org/current/userguide/build_environment.html#sec:gradle_properties_and_system_properties for more information.")
         }

         if (!project.hasProperty("valkyrie_link_ip") || !project.hasProperty("valkyrie_zelda_ip"))
         {
            println("Please set valkyrie_link_ip and valkyrie_zelda_ip in ~/.gradle/gradle.properties. See https://docs.gradle.org/current/userguide/build_environment.html#sec:gradle_properties_and_system_properties for more information.")
         }

         ssh.run {
            session(remotes.valkyrieRealtimeComputer) {
               execute("rm -rf " + directory + "/lib")
               execute("mkdir -p " + directory + "/lib")

               configurations.runtime.each {
                  put from: it, into: directory + "/lib"
               }

               put from: jar.archivePath.path, into: directory + "/ValkyrieController.jar"

               def scriptDir = new File(projectDir, "launchScripts/")
               def scriptDirCollection = files { scriptDir.listFiles() }
               put from: scriptDirCollection, into: directory

               scriptDirCollection.each {
                  execute "chmod 777 " + directory + "/" + it.name
               }
            }

            session(remotes.valkyrieNonRealtimeComputer) {
               execute("rm -rf " + directory + "/lib")
               execute("mkdir -p " + directory + "/lib")

               configurations.runtime.each {
                  put from: it, into: directory + "/lib"
               }

               put from: jar.archivePath, into: directory + "/ValkyrieController.jar"

               execute("rm -rf .ihmc/Configurations")
               execute("mkdir -p .ihmc/Configurations")
               put from: file("saved-configurations/defaultREAModuleConfiguration.txt"), into: ".ihmc/Configurations"

               def scriptDir = new File(projectDir, "launchScripts/")
               def scriptDirCollection = files { scriptDir.listFiles() }
               put from: scriptDirCollection, into: directory

               scriptDirCollection.each {
                  execute "chmod 777 " + directory + "/" + it.name
               }
            }
         }
      }
   }

   task deployNetworkProcessor(dependsOn: jar) {
      def projectDir = project.buildscript.sourceFile.parent
      def directory = "valkyrie"

      doLast {
         if (!project.hasProperty("valkyrie_realtime_username") || !project.hasProperty("valkyrie_realtime_password"))
         {
            println("Please set valkyrie_realtime_username and valkyrie_realtime_password in ~/.gradle/gradle.properties. See https://docs.gradle.org/current/userguide/build_environment.html#sec:gradle_properties_and_system_properties for more information.")
         }

         if (!project.hasProperty("valkyrie_link_ip") || !project.hasProperty("valkyrie_zelda_ip"))
         {
            println("Please set valkyrie_link_ip and valkyrie_zelda_ip in ~/.gradle/gradle.properties. See https://docs.gradle.org/current/userguide/build_environment.html#sec:gradle_properties_and_system_properties for more information.")
         }

         ssh.run {
            session(remotes.valkyrieNonRealtimeComputer) {
               execute("rm -rf " + directory + "/lib")
               execute("mkdir -p " + directory + "/lib")
               execute("mkdir -p " + directory + "/Configurations")

               configurations.runtime.each {
                  put from: it, into: directory + "/lib"
               }

               put from: jar.archivePath, into: directory + "/ValkyrieController.jar"
               
               put from: file("saved-configurations/defaultREAModuleConfiguration.txt"), into: ".ihmc/Configurations"

               def scriptDir = new File(projectDir, "launchScripts/")
               def scriptDirCollection = files { scriptDir.listFiles() }
               put from: scriptDirCollection, into: directory

               scriptDirCollection.each {
                  execute "chmod 777 " + directory + "/" + it.name
               }
            }
         }
      }
   }

//task createRosJar(type: Jar) {
//   def gitRepo;
//   if (!project.parent.hasProperty("repo"))
//   {
//      gitRepo = Grgit.open(projectDir.canonicalFile.parent)
//   } else
//   {
//      gitRepo = project.parent.repo
//   }
//   version = gitRepo.head().abbreviatedId;
//   manifest {
//      attributes "Implementation-Title": "ROSAPI",
//            "Implementation-Version": version,
//            "Main-Class": "us.ihmc.valkyrie.OpenHumanoidsSimulator"
//   }
//
//   baseName = "OpenHumanoidsSimulator"
//   from({ configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }) {
//      exclude "META-INF/*.SF"
//      exclude "META-INF/*.DSA"
//      exclude "META-INF/*.RSA"
//      with jar
//   }
//}

//task OH(type: Copy, dependsOn: [createRosJar]) {
//   from createRosJar
//   into "lib/"
//}
}
