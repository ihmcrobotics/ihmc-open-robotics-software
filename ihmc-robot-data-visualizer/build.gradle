plugins {
   id("us.ihmc.ihmc-build") version "0.19.5"
   id("us.ihmc.ihmc-ci") version "4.25"
   id("us.ihmc.ihmc-cd") version "0.1"
   id("us.ihmc.log-tools") version "0.3.1"
   id("us.ihmc.scs") version "0.4"
   id "org.hidetake.ssh" version "2.9.0"
   id "application"
}

ihmc {
   loadProductProperties("../product.properties")

   configureDependencyResolution()
   configurePublications()
}

mainDependencies {
   compile("net.sf.trove4j:trove4j:3.0.3")
   compile("com.martiansoftware:jsap:2.1")
   compile("net.sourceforge.jmatio:jmatio:1.0")

   compile("us.ihmc:euclid:0.12.1")
   compile("us.ihmc:ihmc-yovariables:0.3.11")
   compile("us.ihmc:ihmc-video-codecs:2.1.5")
   compile("us.ihmc:ihmc-commons:0.26.6")
   compile("us.ihmc:simulation-construction-set:0.12.15")
   compile("us.ihmc:ihmc-javafx-toolkit:0.12.9")
   compile("us.ihmc:ihmc-graphics-description:0.12.12")
   compile("us.ihmc:ihmc-robot-description:0.12.7")
   compile("us.ihmc:ihmc-model-file-loader:source")
   compile("us.ihmc:ihmc-communication:source")
   compile("us.ihmc:ihmc-java-toolkit:source")
   compile("us.ihmc:ihmc-humanoid-robotics:source")
   compile("us.ihmc:ihmc-robotics-toolkit:source")
   compile("us.ihmc:ihmc-robot-data-logger:source")
}

testDependencies {
    compile("us.ihmc:ihmc-robotics-toolkit-test:source")
}

mainClassName = hasProperty("mainClass") ? mainClass : "us.ihmc.robotDataVisualizer.visualizer.SCSVisualizer"

if (ihmc.isBuildRoot())
{
   task viewerStartScripts(type: CreateStartScripts) {
      outputDir = file("build/scripts")
      mainClassName = "us.ihmc.robotDataVisualizer.gui.GUICaptureViewer"
      applicationName = "IHMCGUICaptureViewer"
      classpath = project.configurations.runtime + jar.outputs.files
   }

   distributions {
      guiCaptureViewer {
         baseName = "IHMCGUICaptureViewer"
         contents {
            into("lib") {
               from project.configurations.runtime + jar.outputs.files
            }

            into("bin") {
               from viewerStartScripts
               include "IHMCGUICaptureViewer*"
            }
         }
      }
   }

   task setupDeployViewerRemote {
      doLast {
         def deployViewerUserValue = project.hasProperty("deployViewerUser") ? project.deployViewerUser : "invalid"
         def deployViewerPasswordValue = project.hasProperty("deployViewerPassword") ? project.deployViewerPassword : "invalid"
         def deployViewerHostValue = project.hasProperty("deployViewerHost") ? project.deployViewerHost : "invalid"

         remotes.create("deployViewerTarget") {
            host = deployViewerHostValue
            user = deployViewerUserValue
            password = deployViewerPasswordValue
            knownHosts = allowAnyHosts
         }
      }
   }

   task deployViewer(dependsOn: [guiCaptureViewerDistTar, setupDeployViewerRemote]) {
      doLast {
         ssh.run {
            session(remotes.deployViewerTarget) {
               project.logger.lifecycle("Copying GUI Capture Viewer distribution tarball to remote host")
               def distTarFile = guiCaptureViewerDistTar.outputs.files.singleFile
               put from: distTarFile, into: "."

               project.logger.lifecycle("Untarring distribution on remote host")
               execute "tar xf ./${distTarFile.name}"
               project.logger.lifecycle("Removing tarball from remote host")
               execute "rm -f ./${distTarFile.name}"
               project.logger.lifecycle("Moving GUI Capture Viewer distribution in to place")
               execute "mv ./${distTarFile.name.replace(".tar", "")} IHMCGUICaptureViewer"
               project.logger.lifecycle("GUI Capture Viewer deployment to remote host complete!")
            }
         }
      }
   }
}
