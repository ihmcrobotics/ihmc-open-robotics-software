plugins {
   id("us.ihmc.ihmc-build") version "0.18.5"
   id("us.ihmc.ihmc-ci") version "4.25"
   id("us.ihmc.ihmc-cd") version "0.1"
   id("us.ihmc.log-tools") version "0.3.1"
}

ihmc {
   loadProductProperties("../product.properties")
   maintainer = "Daniel Duran (dduran@ihmc.us)"

   configureDependencyResolution()
   configurePublications()
}

mainDependencies {
   compile("org.jgrapht:jgrapht-core:0.9.0")

   compile("us.ihmc:ihmc-commons:0.26.6")
   compile("us.ihmc:euclid-frame:0.12.0")
   compile("us.ihmc:euclid-shape:0.12.0")
   compile("us.ihmc:ihmc-javafx-toolkit:0.12.9")
   compile("us.ihmc:ihmc-yovariables:0.3.11")
   compile("us.ihmc:ihmc-convex-optimization-adapter:source")
   compile("us.ihmc:robot-environment-awareness:source")
}

visualizersDependencies {
   compile ihmc.sourceSetProject("main")
   compile ihmc.sourceSetProject("data-sets")

   compile("us.ihmc:ihmc-javafx-toolkit:0.12.9")
   compile("us.ihmc:robot-environment-awareness-application:source")
   compile("us.ihmc:ihmc-communication:source")
   compile("us.ihmc:ihmc-robot-models-visualizers:source")
}

testDependencies {
   compile ihmc.sourceSetProject("visualizers")
   compile ihmc.sourceSetProject("data-sets")

   compile("us.ihmc:ihmc-commons-testing:0.26.6")
   compile("us.ihmc:simulation-construction-set:0.12.15")
   compile("us.ihmc:robot-environment-awareness-application:source")
   compile("us.ihmc:ihmc-robotics-toolkit-test:source")
}

dataSetsDependencies {
   compile ihmc.sourceSetProject("main")

   compile("us.ihmc:ihmc-commons-testing:0.26.6")
   compile("us.ihmc:simulation-construction-set-tools:source")
   compile("us.ihmc:robot-environment-awareness:source")
}

task updateDataSetList {
   doFirst {
      def resourcesDir = "src/data-sets/resources/us/ihmc/pathPlanning/dataSets"
      def srcDir = "src/data-sets/java/us/ihmc/pathPlanning"
      def className = "DataSetName"

      def dataSetDir = new File(resourcesDir)
      def dataSetList = files { dataSetDir.listFiles().sort().reverse() }

      File classFile = new File(srcDir + "/" + className + ".java")
      println classFile.getAbsolutePath()

      if(classFile.exists())
         classFile.delete()
      classFile.createNewFile()

      FileWriter fileWriter = new FileWriter(classFile, true)
      fileWriter.write("package us.ihmc.pathPlanning;\n\n")
      fileWriter.write("public enum " + className + "\n")
      fileWriter.write("{\n")

      dataSetList.each {
         fileWriter.write("\t_" + it.name + ",\n")
      }

      fileWriter.write("}")
      fileWriter.flush()
      fileWriter.close()
   }
}